<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
	xmlns:fo="http://www.w3.org/1999/XSL/Format"
	xmlns:fox="http://xmlgraphics.apache.org/fop/extensions"
	xmlns:dss="http://dss.esig.europa.eu/validation/simple-certificate-report">
	<xsl:output method="xml" indent="yes" />

	<xsl:param name="rootUrlInTlBrowser">https://eidas.ec.europa.eu/efda/tl-browser/#/screen</xsl:param>
	<xsl:param name="euTLSubDirectoryInTlBrowser">/tl</xsl:param>
	<xsl:param name="tcTLSubDirectoryInTlBrowser">/tc-tl</xsl:param>
	<xsl:param name="trustmarkSubDirectoryInTlBrowser">/trustmark</xsl:param>
	<xsl:param name="euGenericTSLType">http://uri.etsi.org/TrstSvc/TrustedList/TSLType/EUgeneric</xsl:param>

	<xsl:template match="/dss:SimpleCertificateReport">
		<fo:root>
			<xsl:attribute name="font-family">sans-serif</xsl:attribute>
			
			<fo:layout-master-set>
				<fo:simple-page-master>
					<xsl:attribute name="master-name">A4-portrait</xsl:attribute>
					<xsl:attribute name="page-height">29.7cm</xsl:attribute>
					<xsl:attribute name="page-width">21cm</xsl:attribute>
					<xsl:attribute name="margin-top">1cm</xsl:attribute>
					<xsl:attribute name="margin-bottom">1cm</xsl:attribute>
					<xsl:attribute name="margin-right">2.5cm</xsl:attribute>
					<xsl:attribute name="margin-left">2.5cm</xsl:attribute>
			
					<fo:region-body>
						<xsl:attribute name="margin-top">1cm</xsl:attribute>
						<xsl:attribute name="margin-bottom">1cm</xsl:attribute>
					</fo:region-body>

					<fo:region-after>
						<xsl:attribute name="region-name">page-footer</xsl:attribute>
						<xsl:attribute name="extent">0.5cm</xsl:attribute>
					</fo:region-after>
				</fo:simple-page-master>
			</fo:layout-master-set>

			<fo:page-sequence>
				<xsl:attribute name="master-reference">A4-portrait</xsl:attribute>
	
				<fo:static-content>
					<xsl:attribute name="flow-name">page-footer</xsl:attribute>
					<xsl:attribute name="font-size">5pt</xsl:attribute>
					
					<fo:block>
						<xsl:attribute name="color">grey</xsl:attribute>
						<xsl:attribute name="border-top-style">solid</xsl:attribute>
						<xsl:attribute name="border-top-color">grey</xsl:attribute>
						<xsl:attribute name="text-align-last">justify</xsl:attribute>
						<xsl:attribute name="padding-top">3px</xsl:attribute>
					
						<fo:inline>
							 <fo:basic-link>
							 	<xsl:attribute name="external-destination">url('https://github.com/esig/dss')</xsl:attribute>
							 	Generated by DSS v.${project.version}
							 </fo:basic-link>
							 <xsl:text>with validation time </xsl:text><xsl:value-of select="@ValidationTime" />
						</fo:inline>
						
						<fo:leader/>

						<fo:inline>
							<fo:page-number />
							/
							<fo:page-number-citation>
								<xsl:attribute name="ref-id">theEnd</xsl:attribute>
							</fo:page-number-citation> 
						</fo:inline>
					</fo:block>
				</fo:static-content>

				<fo:flow>
					<xsl:attribute name="flow-name">xsl-region-body</xsl:attribute>
					<xsl:attribute name="font-size">8pt</xsl:attribute>
					
					<xsl:apply-templates select="dss:Certificate"/>
					<xsl:apply-templates select="dss:ConnectionDetails"/>
	    			
					<fo:block>
						<xsl:attribute name="id">theEnd</xsl:attribute>
					</fo:block>
				</fo:flow>
				
			</fo:page-sequence>

		</fo:root>
		
	</xsl:template>

	<xsl:template match="dss:Chain">
		<xsl:if test="dss:ChainItem">
			<fo:block-container>
				<fo:block>
					<xsl:attribute name="margin-top">7px</xsl:attribute>
					<xsl:attribute name="margin-bottom">2px</xsl:attribute>
					<xsl:attribute name="font-size">8pt</xsl:attribute>

					<xsl:attribute name="font-weight">bold</xsl:attribute>
					Certificate chain:
				</fo:block>
			</fo:block-container>
			<fo:block-container>
				<fo:block>
					<xsl:apply-templates />
				</fo:block>
			</fo:block-container>
		</xsl:if>
	</xsl:template>
    
    <xsl:template match="dss:Certificate|dss:ChainItem">

		<fo:table table-layout="fixed">
			<xsl:attribute name="margin-top">7px</xsl:attribute>
			<xsl:attribute name="margin-bottom">5px</xsl:attribute>

			<fo:table-body>
				<xsl:attribute name="start-indent">0</xsl:attribute>
				<xsl:attribute name="end-indent">0</xsl:attribute>

				<fo:table-row>

					<xsl:attribute name="font-weight">bold</xsl:attribute>

					<xsl:attribute name="border-bottom-style">solid</xsl:attribute>
					<xsl:attribute name="border-color">#004494</xsl:attribute>
					<xsl:attribute name="border-width">1px</xsl:attribute>

					<xsl:attribute name="margin-bottom">2px</xsl:attribute>

					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="font-weight">bold</xsl:attribute>

							<xsl:text>Certificate: </xsl:text><xsl:value-of select="@Id"/>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>

			</fo:table-body>
		</fo:table>

		<fo:block-container>
			<xsl:attribute name="border-left-style">solid</xsl:attribute>
			<xsl:attribute name="border-color">#004494</xsl:attribute>
			<xsl:attribute name="border-width">1px</xsl:attribute>

			<xsl:attribute name="margin-top">7px</xsl:attribute>
			<xsl:attribute name="margin-bottom">5px</xsl:attribute>

			<fo:block-container>
				<xsl:attribute name="margin-left">10px</xsl:attribute>

				<fo:block-container>
					<xsl:attribute name="margin">0</xsl:attribute>

					<fo:block>
						<xsl:attribute name="margin-top">5px</xsl:attribute>
						<xsl:attribute name="margin-bottom">5px</xsl:attribute>
						<xsl:attribute name="font-size">7pt</xsl:attribute>

						<fo:table table-layout="fixed">
							<fo:table-column>
								<xsl:attribute name="column-width">27%</xsl:attribute>
							</fo:table-column>
							<fo:table-column>
								<xsl:attribute name="column-width">73%</xsl:attribute>
							</fo:table-column>

							<fo:table-body>
								<xsl:attribute name="start-indent">0</xsl:attribute>
								<xsl:attribute name="end-indent">0</xsl:attribute>

								<xsl:apply-templates select="dss:qualificationAtIssuance"/>
								<xsl:apply-templates select="dss:qualificationDetailsAtIssuance"/>
								<xsl:apply-templates select="dss:qualificationAtValidation"/>
								<xsl:apply-templates select="dss:qualificationDetailsAtValidation"/>
								<xsl:apply-templates select="dss:enactedMRA"/>
								<xsl:apply-templates select="dss:qwacProfile"/>
								<xsl:apply-templates select="dss:qwacDetails"/>

								<fo:table-row>
									<xsl:variable name="indicationText" select="dss:Indication/text()"/>
									<xsl:variable name="indicationColor">
										<xsl:choose>
											<xsl:when test="$indicationText='PASSED'">green</xsl:when>
											<xsl:when test="$indicationText='INDETERMINATE'">orange</xsl:when>
											<xsl:when test="$indicationText='FAILED'">red</xsl:when>
										</xsl:choose>
									</xsl:variable>

									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>
											<xsl:attribute name="font-weight">bold</xsl:attribute>
											<xsl:text>Indication:</xsl:text>
										</fo:block>
									</fo:table-cell>
									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>

											<xsl:attribute name="font-weight">bold</xsl:attribute>
											<xsl:attribute name="font-size">7pt</xsl:attribute>

											<xsl:attribute name="color"><xsl:value-of select="$indicationColor" /></xsl:attribute>
											<xsl:variable name="subIndication"><xsl:value-of select="dss:SubIndication" /></xsl:variable>
											<xsl:value-of select="$indicationText" /><xsl:if test="$subIndication != ''"> - <xsl:value-of select="dss:SubIndication" /></xsl:if>
										</fo:block>
									</fo:table-cell>
								</fo:table-row>

								<xsl:apply-templates select="dss:X509ValidationDetails" />

								<xsl:apply-templates select="dss:subject" />
								<xsl:apply-templates select="dss:commonName" />
								<xsl:apply-templates select="dss:surname" />
								<xsl:apply-templates select="dss:givenName" />
								<xsl:apply-templates select="dss:pseudonym" />
								<xsl:apply-templates select="dss:organizationName" />
								<xsl:apply-templates select="dss:organizationUnit" />
								<xsl:apply-templates select="dss:email" />
								<xsl:apply-templates select="dss:locality" />
								<xsl:apply-templates select="dss:state" />
								<xsl:apply-templates select="dss:country" />
								<xsl:apply-templates select="dss:issuerId" />
								<xsl:apply-templates select="dss:notBefore" />
								<xsl:apply-templates select="dss:notAfter" />

								<xsl:apply-templates select="dss:keyUsages" />
								<xsl:apply-templates select="dss:extendedKeyUsages" />
								<xsl:apply-templates select="dss:ocspUrls" />
								<xsl:apply-templates select="dss:crlUrls" />
								<xsl:apply-templates select="dss:aiaUrls" />
								<xsl:apply-templates select="dss:cpsUrls" />
								<xsl:apply-templates select="dss:pdsUrls" />

								<xsl:apply-templates select="dss:revocation" />

								<xsl:apply-templates select="dss:trustAnchors" />
								<xsl:apply-templates select="dss:trustStartDate" />
								<xsl:apply-templates select="dss:trustSunsetDate" />

							</fo:table-body>
						</fo:table>

						<xsl:apply-templates select="dss:Chain" />
						<xsl:apply-templates select="dss:TLSBindingSignature" />

					</fo:block>
				</fo:block-container>
			</fo:block-container>

		</fo:block-container>
		
    </xsl:template>

	<xsl:template match="dss:X509ValidationDetails">
		<fo:table-row>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:attribute name="font-weight">bold</xsl:attribute>
					X509 Validation Details:
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>

				<xsl:apply-templates select="dss:Error" />
				<xsl:apply-templates select="dss:Warning" />
				<xsl:apply-templates select="dss:Info" />

			</fo:table-cell>
		</fo:table-row>
	</xsl:template>

	<xsl:template match="dss:qualificationDetailsAtIssuance|dss:qualificationDetailsAtValidation|dss:qwacDetails">
		<fo:table-row>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>
			<fo:table-cell>
				<fo:block/>
			</fo:table-cell>
			<fo:table-cell>

				<xsl:apply-templates select="dss:Error" />
				<xsl:apply-templates select="dss:Warning" />
				<xsl:apply-templates select="dss:Info" />

			</fo:table-cell>
		</fo:table-row>
	</xsl:template>

	<xsl:template match="dss:Error|dss:Warning|dss:Info">
		<xsl:variable name="indicationColor">
			<xsl:choose>
				<xsl:when test="name() = 'Error'">red</xsl:when>
				<xsl:when test="name() = 'Warning'">orange</xsl:when>
				<xsl:otherwise>black</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
		<fo:block>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>

			<xsl:attribute name="color"><xsl:value-of select="$indicationColor" /></xsl:attribute>
			<xsl:value-of select="." />
		</fo:block>
	</xsl:template>

	<xsl:template match="dss:subject">
		<xsl:apply-templates/>
	</xsl:template>
    
    <xsl:template match="dss:commonName|dss:surname|dss:givenName|dss:pseudonym|dss:organizationName|
    		dss:organizationUnit|dss:email|dss:locality|dss:state|dss:country|dss:issuerId">
		 <xsl:variable name="label">
        	<xsl:choose>
				<xsl:when test="name()='commonName'">Common name</xsl:when>
				<xsl:when test="name()='surname'">Surname</xsl:when>
				<xsl:when test="name()='givenName'">Given name</xsl:when>
				<xsl:when test="name()='pseudonym'">Pseudonym</xsl:when>
				<xsl:when test="name()='organizationName'">Organization name</xsl:when>
				<xsl:when test="name()='organizationUnit'">Organization unit</xsl:when>
				<xsl:when test="name()='email'">Email</xsl:when>
				<xsl:when test="name()='locality'">Locality</xsl:when>
				<xsl:when test="name()='state'">State</xsl:when>
				<xsl:when test="name()='country'">Country</xsl:when>
				<xsl:when test="name()='issuerId'">Issuer Id</xsl:when>
				<xsl:otherwise>?</xsl:otherwise>
			</xsl:choose>
        </xsl:variable>
        
		<fo:table-row>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					
					<xsl:attribute name="font-weight">bold</xsl:attribute>
					<xsl:value-of select="$label" /><xsl:text>:</xsl:text>
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:value-of select="." />
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
    </xsl:template>

	<xsl:template match="dss:notBefore|dss:notAfter|dss:trustStartDate|dss:trustSunsetDate">
		<xsl:variable name="label">
			<xsl:choose>
				<xsl:when test="name()='notBefore'">Not Before</xsl:when>
				<xsl:when test="name()='notAfter'">Not After</xsl:when>
				<xsl:when test="name()='trustStartDate'">Trust start date</xsl:when>
				<xsl:when test="name()='trustSunsetDate'">Trust sunset date</xsl:when>
				<xsl:otherwise>?</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>

		<fo:table-row>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:attribute name="font-weight">bold</xsl:attribute>
					<xsl:value-of select="$label" /><xsl:text>:</xsl:text>
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:call-template name="formatdate"><xsl:with-param name="DateTimeStr" select="."/></xsl:call-template>
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
	</xsl:template>
    
    <xsl:template match="dss:keyUsages">
    	<xsl:if test="node() != ''">
			<fo:table-row>
				<fo:table-cell>
					<fo:block>
						<xsl:attribute name="margin-top">1px</xsl:attribute>
						<xsl:attribute name="margin-bottom">1px</xsl:attribute>
						
						<xsl:attribute name="font-weight">bold</xsl:attribute>
						<xsl:text>Key usages:</xsl:text>
					</fo:block>
				</fo:table-cell>
				<fo:table-cell>
					<xsl:for-each select="dss:keyUsage">
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:value-of select="." />
						</fo:block>
					</xsl:for-each>
				</fo:table-cell>
			</fo:table-row>
		</xsl:if>
    </xsl:template>
    
    <xsl:template match="dss:extendedKeyUsages|dss:ocspUrls|dss:crlUrls|dss:aiaUrls|dss:cpsUrls|dss:pdsUrls">
    	<xsl:if test="node() != ''">
			 <xsl:variable name="label">
	        	<xsl:choose>
					<xsl:when test="name()='extendedKeyUsages'">Extended key usages</xsl:when>
					<xsl:when test="name()='ocspUrls'">OCSP urls</xsl:when>
					<xsl:when test="name()='crlUrls'">CRL urls</xsl:when>
					<xsl:when test="name()='aiaUrls'">AIA urls</xsl:when>
					<xsl:when test="name()='cpsUrls'">CPS urls</xsl:when>
					<xsl:when test="name()='pdsUrls'">PDS urls</xsl:when>
					<xsl:otherwise>?</xsl:otherwise>
				</xsl:choose>
	        </xsl:variable>
	        
			<fo:table-row>
				<fo:table-cell>
					<fo:block>
						<xsl:attribute name="margin-top">1px</xsl:attribute>
						<xsl:attribute name="margin-bottom">1px</xsl:attribute>
						
						<xsl:attribute name="font-weight">bold</xsl:attribute>
						<xsl:value-of select="$label" /><xsl:text>:</xsl:text>
					</fo:block>
				</fo:table-cell>
				<fo:table-cell>
					<xsl:apply-templates/>
				</fo:table-cell>
			</fo:table-row>
		</xsl:if>
    </xsl:template>
    
    <xsl:template match="dss:extendedKeyUsage">
    	<fo:block>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>
			
			<xsl:value-of select="." />
		</fo:block>
    </xsl:template>
    
    <xsl:template match="dss:ocspUrl|dss:crlUrl|dss:aiaUrl|dss:cpsUrl|dss:pdsUrl">
		<xsl:variable name="url" select="." />
    	<fo:block>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>
			
			<fo:basic-link>
			 	<xsl:attribute name="external-destination"><xsl:value-of select="$url" /></xsl:attribute>
			 	<xsl:value-of select="$url" />
			 </fo:basic-link>
		</fo:block>
    </xsl:template>
    
    <xsl:template match="dss:qualificationAtIssuance|dss:qualificationAtValidation">
		 <xsl:variable name="label">
        	<xsl:choose>
				<xsl:when test="name()='qualificationAtIssuance'">Qualification at issuance time</xsl:when>
				<xsl:when test="name()='qualificationAtValidation'">Qualification at validation time</xsl:when>
				<xsl:otherwise>?</xsl:otherwise>
			</xsl:choose>
        </xsl:variable>
        
		<fo:table-row>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					
					<xsl:attribute name="font-weight">bold</xsl:attribute>
					<xsl:value-of select="$label" /><xsl:text>:</xsl:text>
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					<xsl:attribute name="font-size">7pt</xsl:attribute>
					<xsl:attribute name="font-weight">bold</xsl:attribute>
					
					<xsl:value-of select="." />
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
    </xsl:template>

	<xsl:template match="dss:qwacProfile">
		<fo:table-row>
			<fo:table-cell display-align="center">
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					<xsl:attribute name="alignment-baseline">middle</xsl:attribute>

					<xsl:attribute name="font-weight">bold</xsl:attribute>
					<xsl:text>QWAC Profile:</xsl:text>
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					<xsl:attribute name="font-size">7pt</xsl:attribute>
					<xsl:attribute name="font-weight">bold</xsl:attribute>
					<xsl:attribute name="alignment-baseline">middle</xsl:attribute>

					<xsl:value-of select="." />

					<xsl:if test=".='1-QWAC' or .='2-QWAC'">
						<xsl:text> </xsl:text>
						<fo:instream-foreign-object fox:alt-text="arrow-right" content-height="12px" content-width="8px" height="16px" width="8px">
							<xsl:attribute name="alignment-baseline">middle</xsl:attribute>
							<svg xmlns="http://www.w3.org/2000/svg" width="16px" height="24px" viewBox="0 0 16 24" version="1.1">
								<g id="surface1">
									<path style=" stroke:none;fill-rule:evenodd;fill:rgb(12.941177%,24.705882%,43.529412%);fill-opacity:1;" d="M 7.988281 2.390625 C 9.800781 2.390625 11.277344 3.835938 11.277344 5.613281 C 11.277344 6.703125 11.285156 7.769531 11.300781 8.699219 L 4.699219 8.699219 L 4.699219 5.613281 C 4.699219 3.835938 6.175781 2.390625 7.988281 2.390625 M 13.746094 8.703125 C 13.734375 7.777344 13.722656 6.707031 13.722656 5.613281 C 13.722656 2.511719 11.15625 0 7.988281 0 C 4.820312 0 2.25 2.511719 2.25 5.613281 L 2.25 8.707031 C 0.992188 8.804688 0 9.835938 0 11.089844 L 0 20.648438 C 0 21.96875 1.09375 23.039062 2.445312 23.039062 L 13.605469 23.039062 C 14.957031 23.039062 16.054688 21.96875 16.054688 20.648438 L 16.054688 11.089844 C 16.054688 9.816406 15.03125 8.773438 13.746094 8.703125 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 8.027344 9.816406 L 7.859375 10.320312 L 7.347656 10.320312 L 7.753906 10.644531 L 7.605469 11.148438 L 8.027344 10.847656 L 8.449219 11.148438 L 8.300781 10.644531 L 8.707031 10.320312 L 8.195312 10.320312 L 8.027344 9.816406 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 2.613281 13.96875 L 3.035156 13.667969 L 3.457031 13.96875 L 3.308594 13.464844 L 3.714844 13.140625 L 3.203125 13.140625 L 3.035156 12.636719 L 2.867188 13.140625 L 2.355469 13.140625 L 2.761719 13.464844 L 2.613281 13.96875 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 3.351562 18.351562 L 3.179688 17.847656 L 3.011719 18.351562 L 2.5 18.351562 L 2.910156 18.675781 L 2.761719 19.179688 L 3.179688 18.878906 L 3.601562 19.179688 L 3.453125 18.675781 L 3.863281 18.351562 L 3.351562 18.351562 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 5.089844 10.582031 L 4.921875 11.085938 L 4.410156 11.085938 L 4.816406 11.410156 L 4.667969 11.914062 L 5.089844 11.613281 L 5.511719 11.914062 L 5.363281 11.410156 L 5.769531 11.085938 L 5.257812 11.085938 L 5.089844 10.582031 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 5.257812 20.359375 L 5.089844 19.855469 L 4.921875 20.359375 L 4.410156 20.359375 L 4.816406 20.683594 L 4.667969 21.1875 L 5.089844 20.886719 L 5.511719 21.1875 L 5.363281 20.683594 L 5.769531 20.359375 L 5.257812 20.359375 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 12.546875 13.96875 L 12.96875 13.667969 L 13.390625 13.96875 L 13.242188 13.464844 L 13.652344 13.140625 L 13.140625 13.140625 L 12.96875 12.636719 L 12.800781 13.140625 L 12.289062 13.140625 L 12.699219 13.464844 L 12.546875 13.96875 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 13.140625 18.351562 L 12.96875 17.847656 L 12.800781 18.351562 L 12.289062 18.351562 L 12.695312 18.675781 L 12.546875 19.179688 L 12.96875 18.878906 L 13.390625 19.179688 L 13.242188 18.675781 L 13.652344 18.351562 L 13.140625 18.351562 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 10.964844 10.582031 L 10.792969 11.085938 L 10.28125 11.085938 L 10.691406 11.410156 L 10.542969 11.914062 L 10.964844 11.613281 L 11.386719 11.914062 L 11.234375 11.410156 L 11.644531 11.085938 L 11.132812 11.085938 L 10.964844 10.582031 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 11.132812 20.167969 L 10.964844 19.664062 L 10.792969 20.167969 L 10.28125 20.167969 L 10.691406 20.492188 L 10.542969 20.996094 L 10.964844 20.695312 L 11.386719 20.996094 L 11.234375 20.492188 L 11.644531 20.167969 L 11.132812 20.167969 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 8.195312 21.027344 L 8.027344 20.523438 L 7.859375 21.027344 L 7.347656 21.027344 L 7.753906 21.351562 L 7.605469 21.855469 L 8.027344 21.554688 L 8.449219 21.855469 L 8.300781 21.351562 L 8.707031 21.027344 L 8.195312 21.027344 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 14.140625 15.628906 L 13.628906 15.628906 L 13.460938 15.121094 L 13.289062 15.628906 L 12.777344 15.628906 L 13.1875 15.949219 L 13.039062 16.453125 L 13.460938 16.152344 L 13.878906 16.453125 L 13.730469 15.949219 L 14.140625 15.628906 "/>
									<path style=" stroke:none;fill-rule:nonzero;fill:rgb(100%,100%,100%);fill-opacity:1;" d="M 2.59375 16.152344 L 3.015625 16.453125 L 2.867188 15.949219 L 3.273438 15.628906 L 2.761719 15.628906 L 2.59375 15.121094 L 2.425781 15.628906 L 1.914062 15.628906 L 2.320312 15.949219 L 2.171875 16.457031 L 2.59375 16.152344 "/>
									<path style=" stroke:none;fill-rule:evenodd;fill:rgb(98.431373%,78.431374%,6.666667%);fill-opacity:1;" d="M 14.367188 10.179688 C 13.914062 10.4375 13.058594 11.113281 11.800781 12.207031 C 10.632812 13.222656 9.28125 14.480469 7.75 15.976562 L 5.109375 13.730469 L 4.109375 14.6875 C 6.121094 16.652344 7.554688 18.160156 8.410156 19.214844 C 9.375 17.65625 10.460938 16.097656 11.660156 14.53125 C 12.859375 12.96875 13.984375 11.734375 15.027344 10.824219 C 14.800781 10.382812 14.601562 10.257812 14.367188 10.179688 "/>
								</g>
							</svg>
						</fo:instream-foreign-object>
					</xsl:if>
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
	</xsl:template>
    
    <xsl:template match="dss:enactedMRA">
        
		<fo:table-row>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>
					<xsl:attribute name="font-size">7pt</xsl:attribute>
					<xsl:attribute name="font-weight">bold</xsl:attribute>
					The qualification level has been determined using an enacted trust service equivalence mapping.
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
    </xsl:template>
    
    <xsl:template match="dss:revocation">
    
	   	<xsl:choose>
			<xsl:when test="dss:thisUpdate">
	    		<fo:table-row>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:attribute name="font-weight">bold</xsl:attribute>
							<xsl:text>Revocation thisUpdate time:</xsl:text>
						</fo:block>
					</fo:table-cell>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:value-of select="dss:thisUpdate" />
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			   	<fo:table-row>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:attribute name="font-weight">bold</xsl:attribute>
							<xsl:text>Revocation status:</xsl:text>
						</fo:block>
					</fo:table-cell>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							
						   	<xsl:choose>
								<xsl:when test="dss:revocationDate">
						    		<xsl:text>Revoked (reason: </xsl:text><xsl:value-of select="dss:revocationReason" /> @ <xsl:value-of select="dss:revocationDate" />)
								</xsl:when>
								<xsl:otherwise>
						     		<xsl:text>Not revoked</xsl:text>
								</xsl:otherwise>
						   	</xsl:choose>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
	    	</xsl:when>
			<xsl:when test="not(../dss:trustAnchors)">
	     		<fo:table-row>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:attribute name="font-weight">bold</xsl:attribute>
							<xsl:text>Revocation:</xsl:text>
						</fo:block>
					</fo:table-cell>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<xsl:text>No revocation data available</xsl:text>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</xsl:when>
    	</xsl:choose>
    	
    </xsl:template>
    
    <xsl:template match="dss:trustAnchors">
    	<xsl:if test="node() != ''">
	  		<fo:table-row>
				<fo:table-cell>
					<fo:block>
						<xsl:attribute name="margin-top">1px</xsl:attribute>
						<xsl:attribute name="margin-bottom">1px</xsl:attribute>
						
						<xsl:attribute name="font-weight">bold</xsl:attribute>
						<xsl:text>Trust anchor(s):</xsl:text>
					</fo:block>
				</fo:table-cell>
				<fo:table-cell>
					<xsl:attribute name="display-align">center</xsl:attribute>
					
			    	<xsl:for-each select="dss:trustAnchor">
						<xsl:variable name="subDirectory">
							<xsl:choose>
								<xsl:when test="dss:tslType and $euGenericTSLType = dss:tslType"><xsl:value-of select="$euTLSubDirectoryInTlBrowser" /></xsl:when>
								<xsl:otherwise><xsl:value-of select="$tcTLSubDirectoryInTlBrowser" /></xsl:otherwise>
							</xsl:choose>
						</xsl:variable>
						<xsl:variable name="countryTlUrl" select="concat($rootUrlInTlBrowser, $subDirectory, '/', dss:countryCode)" />
						<xsl:variable name="countryTspUrl" select="concat($rootUrlInTlBrowser, $subDirectory,
								$trustmarkSubDirectoryInTlBrowser, '/', dss:countryCode, '/', dss:trustServiceProviderRegistrationId)" />

						<fo:block>
							<xsl:attribute name="margin-top">1px</xsl:attribute>
							<xsl:attribute name="margin-bottom">1px</xsl:attribute>
							
							<fo:basic-link>
								<xsl:attribute name="external-destination"><xsl:value-of select="$countryTlUrl"/></xsl:attribute>
								<xsl:value-of select="dss:countryCode" />
							</fo:basic-link>
							<xsl:text> </xsl:text>
							<fo:instream-foreign-object fox:alt-text="arrow-right" content-height="6px" content-width="6px" height="6px" width="6px">
								<svg viewBox="0 0 1792 1792" xmlns="http://www.w3.org/2000/svg"><path style="fill:black" d="M1413 896q0-27-18-45l-91-91-362-362q-18-18-45-18t-45 18l-91 91q-18 18-18 45t18 45l189 189h-502q-26 0-45 19t-19 45v128q0 26 19 45t45 19h502l-189 189q-19 19-19 45t19 45l91 91q18 18 45 18t45-18l362-362 91-91q18-18 18-45zm251 0q0 209-103 385.5t-279.5 279.5-385.5 103-385.5-103-279.5-279.5-103-385.5 103-385.5 279.5-279.5 385.5-103 385.5 103 279.5 279.5 103 385.5z"/></svg>
							</fo:instream-foreign-object>
							<xsl:text> </xsl:text>
							<fo:basic-link>
								<xsl:attribute name="external-destination"><xsl:value-of select="$countryTspUrl"/></xsl:attribute>
							<xsl:value-of select="dss:trustServiceProvider" />
							</fo:basic-link>
							<xsl:text> </xsl:text>
							<fo:instream-foreign-object fox:alt-text="arrow-right" content-height="6px" content-width="6px" height="6px" width="6px">
								<svg viewBox="0 0 1792 1792" xmlns="http://www.w3.org/2000/svg"><path style="fill:black" d="M1413 896q0-27-18-45l-91-91-362-362q-18-18-45-18t-45 18l-91 91q-18 18-18 45t18 45l189 189h-502q-26 0-45 19t-19 45v128q0 26 19 45t45 19h502l-189 189q-19 19-19 45t19 45l91 91q18 18 45 18t45-18l362-362 91-91q18-18 18-45zm251 0q0 209-103 385.5t-279.5 279.5-385.5 103-385.5-103-279.5-279.5-103-385.5 103-385.5 279.5-279.5 385.5-103 385.5 103 279.5 279.5 103 385.5z"/></svg>
							</fo:instream-foreign-object>
							<xsl:text> </xsl:text>
							<xsl:value-of select="dss:trustServiceName" />
						</fo:block>
			    	</xsl:for-each>
				</fo:table-cell>
			</fo:table-row>
		</xsl:if>
    </xsl:template>

	<xsl:template match="dss:TLSBindingSignature">
		<fo:block-container>
			<fo:block>
				<xsl:attribute name="margin-top">7px</xsl:attribute>
				<xsl:attribute name="margin-bottom">2px</xsl:attribute>
				<xsl:attribute name="font-size">8pt</xsl:attribute>

				<xsl:attribute name="font-weight">bold</xsl:attribute>
				TLS Certificate Binding signature:
			</fo:block>
		</fo:block-container>
		<fo:block-container>
			<fo:block>
				<xsl:variable name="indicationText" select="dss:Indication/text()"/>
				<xsl:variable name="idToken" select="@Id" />
				<xsl:variable name="indicationColor">
					<xsl:choose>
						<xsl:when test="$indicationText='TOTAL_PASSED'">green</xsl:when>
						<xsl:when test="$indicationText='PASSED'">green</xsl:when>
						<xsl:when test="$indicationText='INDETERMINATE'">orange</xsl:when>
						<xsl:when test="$indicationText='FAILED'">red</xsl:when>
						<xsl:when test="$indicationText='TOTAL_FAILED'">red</xsl:when>
					</xsl:choose>
				</xsl:variable>

				<fo:table table-layout="fixed">
					<xsl:attribute name="id">tls-binding-signature</xsl:attribute>

					<xsl:attribute name="margin-top">7px</xsl:attribute>
					<xsl:attribute name="margin-bottom">5px</xsl:attribute>

					<fo:table-body>
						<xsl:attribute name="start-indent">0</xsl:attribute>
						<xsl:attribute name="end-indent">0</xsl:attribute>

						<fo:table-row>

							<xsl:attribute name="font-weight">bold</xsl:attribute>

							<xsl:attribute name="border-bottom-style">solid</xsl:attribute>
							<xsl:attribute name="border-color">#004494</xsl:attribute>
							<xsl:attribute name="border-width">1px</xsl:attribute>

							<xsl:attribute name="margin-bottom">2px</xsl:attribute>

							<fo:table-cell>
								<fo:block>
									<xsl:attribute name="font-weight">bold</xsl:attribute>

									<xsl:text>Signature: </xsl:text><xsl:value-of select="$idToken" />
								</fo:block>
							</fo:table-cell>
						</fo:table-row>

					</fo:table-body>
				</fo:table>

				<fo:block-container>
					<xsl:attribute name="border-left-style">solid</xsl:attribute>
					<xsl:attribute name="border-color">#004494</xsl:attribute>
					<xsl:attribute name="border-width">1px</xsl:attribute>

					<xsl:attribute name="margin-top">5px</xsl:attribute>
					<xsl:attribute name="margin-bottom">5px</xsl:attribute>

					<fo:block-container>
						<xsl:attribute name="margin-left">10px</xsl:attribute>
						<fo:block-container>
							<xsl:attribute name="margin">0</xsl:attribute>

							<fo:table table-layout="fixed">
								<xsl:attribute name="font-size">7pt</xsl:attribute>

								<fo:table-column>
									<xsl:attribute name="column-width">25%</xsl:attribute>
								</fo:table-column>
								<fo:table-column>
									<xsl:attribute name="column-width">75%</xsl:attribute>
								</fo:table-column>
								<fo:table-body>

									<xsl:if test="dss:Url">
										<fo:table-row>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>
											<fo:table-cell>
												<fo:block>
													<xsl:attribute name="margin-top">1px</xsl:attribute>
													<xsl:attribute name="margin-bottom">1px</xsl:attribute>

													<xsl:attribute name="font-weight">bold</xsl:attribute>
													URL:
												</fo:block>
											</fo:table-cell>
											<fo:table-cell>
												<fo:block>
													<xsl:attribute name="margin-top">1px</xsl:attribute>
													<xsl:attribute name="margin-bottom">1px</xsl:attribute>

													<xsl:value-of select="dss:Url" />
												</fo:block>
											</fo:table-cell>
										</fo:table-row>
									</xsl:if>

									<fo:table-row>
										<xsl:attribute name="margin-top">1px</xsl:attribute>
										<xsl:attribute name="margin-bottom">1px</xsl:attribute>
										<fo:table-cell>
											<fo:block>
												<xsl:attribute name="margin-top">1px</xsl:attribute>
												<xsl:attribute name="margin-bottom">1px</xsl:attribute>

												<xsl:attribute name="font-weight">bold</xsl:attribute>
												Indication:
											</fo:block>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block>
												<xsl:attribute name="margin-top">1px</xsl:attribute>
												<xsl:attribute name="margin-bottom">1px</xsl:attribute>

												<xsl:attribute name="font-weight">bold</xsl:attribute>
												<xsl:attribute name="color"><xsl:value-of select="$indicationColor" /></xsl:attribute>
												<xsl:variable name="subIndication"><xsl:value-of select="dss:SubIndication" /></xsl:variable>
												<xsl:value-of select="dss:Indication" /><xsl:if test="$subIndication != ''"> - <xsl:value-of select="dss:SubIndication" /></xsl:if>
											</fo:block>
										</fo:table-cell>
									</fo:table-row>

									<xsl:apply-templates select="dss:AdESValidationDetails" />

									<xsl:if test="@SignatureFormat">
										<fo:table-row>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>
											<fo:table-cell>
												<fo:block>
													<xsl:attribute name="margin-top">1px</xsl:attribute>
													<xsl:attribute name="margin-bottom">1px</xsl:attribute>

													<xsl:attribute name="font-weight">bold</xsl:attribute>
													Signature Format:
												</fo:block>
											</fo:table-cell>
											<fo:table-cell>
												<fo:block>
													<xsl:attribute name="margin-top">1px</xsl:attribute>
													<xsl:attribute name="margin-bottom">1px</xsl:attribute>

													<xsl:value-of select="@SignatureFormat" />
												</fo:block>
											</fo:table-cell>
										</fo:table-row>
									</xsl:if>

									<fo:table-row>
										<xsl:attribute name="margin-top">1px</xsl:attribute>
										<xsl:attribute name="margin-bottom">1px</xsl:attribute>
										<fo:table-cell>
											<fo:block>
												<xsl:attribute name="margin-top">1px</xsl:attribute>
												<xsl:attribute name="margin-bottom">1px</xsl:attribute>

												<xsl:attribute name="font-weight">bold</xsl:attribute>
												On claimed time:
											</fo:block>
										</fo:table-cell>
										<fo:table-cell>
											<fo:block>
												<xsl:attribute name="margin-top">1px</xsl:attribute>
												<xsl:attribute name="margin-bottom">1px</xsl:attribute>

												<xsl:call-template name="formatdate">
													<xsl:with-param name="DateTimeStr" select="dss:SigningTime"/>
												</xsl:call-template>
											</fo:block>
										</fo:table-cell>
									</fo:table-row>

									<xsl:apply-templates select="dss:SignatureScope" />

								</fo:table-body>
							</fo:table>

							<xsl:apply-templates select="dss:Chain" />

						</fo:block-container>
					</fo:block-container>
				</fo:block-container>
			</fo:block>
		</fo:block-container>

	</xsl:template>

	<xsl:template match="dss:SignatureScope">
		<fo:table-row>
			<xsl:attribute name="margin-top">1px</xsl:attribute>
			<xsl:attribute name="margin-bottom">1px</xsl:attribute>
			<xsl:attribute name="page-break-inside">avoid</xsl:attribute>

			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:attribute name="font-weight">bold</xsl:attribute>
					Signature scope:
				</fo:block>
			</fo:table-cell>
			<fo:table-cell>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:value-of select="@name" />	(<xsl:value-of select="@scope" />)
				</fo:block>
				<fo:block>
					<xsl:attribute name="margin-top">1px</xsl:attribute>
					<xsl:attribute name="margin-bottom">1px</xsl:attribute>

					<xsl:value-of select="." />
				</fo:block>
			</fo:table-cell>
		</fo:table-row>
	</xsl:template>

	<xsl:template match="dss:ConnectionDetails">
		<fo:table table-layout="fixed">
			<xsl:attribute name="margin-top">7px</xsl:attribute>

			<fo:table-body>
				<xsl:attribute name="start-indent">0</xsl:attribute>
				<xsl:attribute name="end-indent">0</xsl:attribute>

				<fo:table-row>
					<fo:table-cell>
						<fo:block>
							<xsl:attribute name="keep-with-next">always</xsl:attribute>
							<xsl:attribute name="font-weight">bold</xsl:attribute>

							<xsl:attribute name="border-bottom-style">solid</xsl:attribute>
							<xsl:attribute name="border-color">#004494</xsl:attribute>
							<xsl:attribute name="border-width">1px</xsl:attribute>

							<xsl:attribute name="margin-bottom">2px</xsl:attribute>

							<xsl:attribute name="id">docInfo</xsl:attribute>
							<xsl:text>Connection details</xsl:text>
						</fo:block>
					</fo:table-cell>
				</fo:table-row>
			</fo:table-body>
		</fo:table>

		<fo:block-container>
			<xsl:attribute name="border-left-style">solid</xsl:attribute>
			<xsl:attribute name="border-color">#004494</xsl:attribute>
			<xsl:attribute name="border-width">1px</xsl:attribute>

			<xsl:attribute name="margin-top">2px</xsl:attribute>
			<xsl:attribute name="margin-bottom">2px</xsl:attribute>
			<xsl:attribute name="font-size">7pt</xsl:attribute>

			<fo:block-container>
				<xsl:attribute name="margin-left">10px</xsl:attribute>
				<fo:block-container>
					<xsl:attribute name="margin">0</xsl:attribute>

					<fo:table table-layout="fixed">
						<xsl:attribute name="font-size">7pt</xsl:attribute>

						<xsl:attribute name="page-break-inside">avoid</xsl:attribute>
						<fo:table-column>
							<xsl:attribute name="column-width">25%</xsl:attribute>
						</fo:table-column>
						<fo:table-column>
							<xsl:attribute name="column-width">75%</xsl:attribute>
						</fo:table-column>
						<fo:table-body>
							<xsl:if test="dss:Url">
								<fo:table-row>
									<xsl:attribute name="margin-top">1px</xsl:attribute>
									<xsl:attribute name="margin-bottom">1px</xsl:attribute>
									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>

											<xsl:attribute name="font-weight">bold</xsl:attribute>
											Connection URL:
										</fo:block>
									</fo:table-cell>
									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>

											<xsl:value-of select="dss:Url"/>
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
							</xsl:if>
							<xsl:if test="dss:TLSCertificateBindingLink">
								<fo:table-row>
									<xsl:attribute name="margin-top">1px</xsl:attribute>
									<xsl:attribute name="margin-bottom">1px</xsl:attribute>
									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>

											<xsl:attribute name="font-weight">bold</xsl:attribute>
											TLS Certificate Binding Link URL:
										</fo:block>
									</fo:table-cell>
									<fo:table-cell>
										<fo:block>
											<xsl:attribute name="margin-top">1px</xsl:attribute>
											<xsl:attribute name="margin-bottom">1px</xsl:attribute>

											<xsl:value-of select="dss:TLSCertificateBindingLink"/>
										</fo:block>
									</fo:table-cell>
								</fo:table-row>
							</xsl:if>
						</fo:table-body>
					</fo:table>
				</fo:block-container>
			</fo:block-container>

		</fo:block-container>

	</xsl:template>

	<xsl:template name="formatdate">
		<xsl:param name="DateTimeStr" />

		<xsl:variable name="date">
			<xsl:value-of select="substring-before($DateTimeStr,'T')" />
		</xsl:variable>

		<xsl:variable name="after-T">
			<xsl:value-of select="substring-after($DateTimeStr,'T')" />
		</xsl:variable>

		<xsl:variable name="time">
			<xsl:value-of select="substring-before($after-T,'Z')" />
		</xsl:variable>

		<xsl:choose>
			<xsl:when test="string-length($date) &gt; 0 and string-length($time) &gt; 0">
				<xsl:value-of select="concat($date,' ', $time, ' (UTC)')" />
			</xsl:when>
			<xsl:when test="string-length($date) &gt; 0">
				<xsl:value-of select="$date" />
			</xsl:when>
			<xsl:when test="string-length($time) &gt; 0">
				<xsl:value-of select="$time" />
			</xsl:when>
			<xsl:otherwise>-</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

</xsl:stylesheet>